#!/usr/bin/env zsh

# Performance profiling (uncomment to debug slow startup)
# zmodload zsh/zprof

#
# Path Setup (Native Zsh Deduplication)
#
typeset -U path PATH
path=(
  /usr/local/share/npm/bin
  $HOME/Library/pnpm
  /opt/homebrew/bin
  /opt/homebrew/sbin
  $HOME/.bin
  $HOME/.cargo/bin
  /usr/local/bin
  /usr/bin
  /bin
  /usr/sbin
  /sbin
  $path
)

#
# 1. Environment & Options (Pre-Plugin)
#
[[ -f "$HOME/.config/zsh/exports.zsh" ]] && source "$HOME/.config/zsh/exports.zsh"
[[ -f "$HOME/.config/zsh/options.zsh" ]] && source "$HOME/.config/zsh/options.zsh"

#
# 2. Initialize Completion
#
autoload -Uz compinit
# Check if dump file is older than 24h, if so regenerate
if [[ -n ${ZDOTDIR:-$HOME}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

#
# 3. Antidote Plugin Manager
#
# Determine Homebrew prefix
if [[ -d /opt/homebrew ]]; then
  BREW_PREFIX="/opt/homebrew"
else
  BREW_PREFIX="/usr/local"
fi

# Load Antidote
if [[ -f "$BREW_PREFIX/opt/antidote/share/antidote/antidote.zsh" ]]; then
  source "$BREW_PREFIX/opt/antidote/share/antidote/antidote.zsh"
  # Initialize plugins statically for speed
  antidote load ${ZDOTDIR:-$HOME}/.zsh_plugins.txt
fi

#
# 4. Functions, Aliases, Bindings (Post-Plugin)
#
[[ -f "$HOME/.config/zsh/completion.zsh" ]] && source "$HOME/.config/zsh/completion.zsh"
[[ -f "$HOME/.config/zsh/functions.zsh" ]] && source "$HOME/.config/zsh/functions.zsh"
[[ -f "$HOME/.config/zsh/aliases.zsh" ]] && source "$HOME/.config/zsh/aliases.zsh"
[[ -f "$HOME/.config/zsh/bindings.zsh" ]] && source "$HOME/.config/zsh/bindings.zsh"

#
# Tool Initialization
#

# FNM (Fast Node Manager)
if command -v fnm &>/dev/null; then
  eval "$(fnm env --use-on-cd)"
fi

# Zoxide (Smart cd)
if command -v zoxide &>/dev/null; then
  eval "$(zoxide init zsh --cmd cd)"
fi

# FZF
if command -v fzf &>/dev/null; then
  eval "$(fzf --zsh)"
  
  if command -v fd &>/dev/null; then
    export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
  fi
  
  # Catppuccin Mocha theme for fzf
  export FZF_DEFAULT_OPTS=" \
    --color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
    --color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
    --color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8"
fi

# GitHub CLI Completion
if command -v gh &>/dev/null; then
  eval "$(gh completion -s zsh)"
fi

#
# Theme (Oh My Posh)
#
if command -v oh-my-posh &>/dev/null; then
  eval "$(oh-my-posh init zsh --config $(brew --prefix oh-my-posh)/themes/catppuccin.omp.json)"
fi
source ~/workspace/catppuccin_mocha-zsh-syntax-highlighting.zsh

#
# Startup Info
#
if [[ -z $FASTFETCH_SHOWN ]]; then
  fastfetch --config ~/workspace/fastfetch-config.jsonc
  export FASTFETCH_SHOWN=1
fi

# Clean up stray background jobs
precmd() {
  jobs -pr
}

# zprof